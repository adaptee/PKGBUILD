# Maintainer : Jekyll Wu<adaptee [at] gmail [dot] com>
# Maintainer : osily <ly50247 at gmail dot com>
# Contributor: Lee.MaRS <leemars@gmail.com>

pkgname=qterm
pkgver=0.5.12
pkgrel=1
pkgdesc="BBS client for Linux, written in Qt"
depends=('qt' 'openssl')
optdepends=('phonon')
makedepends=('cmake')
url="http://sourceforge.net/projects/qterm/"
arch=('i686' 'x86_64')
conflicts=('qterm')
replaces=('qterm')
license=('GPL')
source=(http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('a151e1b14bc5ebc70e67d49f40b5332c')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    if [ -d build ] ; then
        rm build -rf
    fi

    mkdir build && cd build

    # temporary workaround with FindQt4.cmake
    sed -i '/QT_MIN_VERSION/d' ../CMakeLists.txt

    cmake -DCMAKE_INSTALL_PREFIX=/usr ../
    make
}

package()
{
    cd ${srcdir}/${pkgname}-${pkgver}/build
    make DESTDIR=${pkgdir} install
}

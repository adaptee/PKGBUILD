# Maintainer : Jekyll Wu  < adaptee at gmail dot com>
# Contributor: Jacco Koning <archlinux@menollo.nl>, with help from qwak

pkgname=usb-creator
pkgver=0.2.28
pkgrel=1
pkgdesc="Create bootable USB from a LiveCD or disc image of Ubuntu. "
arch=('i686' 'x86_64')
url="https://launchpad.net/usb-creator"
license=('GPL3')
depends=(
            'udisks'
            'syslinux'
            'dosfstools'
            'mtools'
            'cdrkit'
            'dbus-python'
        )

optdepends=(
            'kdebindings-python: for KDE frontend'
            'gnome-python: for GNOME frontend'
           )
makedepends=('python-distutils-extra')
source=(    "https://launchpad.net/ubuntu/+archive/primary/+files/usb-creator_${pkgver}.tar.gz"
            "0001-change-the-path-of-parted.patch"
            "0002-remove-blackcode-using-lsb-release-and-python-debian.patch"
        )
md5sums=('e52f2bde66a0c4801f964647b0ddf4f3'
         'bc80c990f59f7cab2db8cfa76746be61'
         '290389e299688274592c5697578ba4cd')

build() {
    local extractdir="${srcdir}/${pkgname}.trunk"

    cd "${extractdir}"

    # command `parted` is located at /usr/sbin/parted in archlinux
    patch -Np1 -i "${srcdir}/0001-change-the-path-of-parted.patch"

    # archlinux can't provide thoso equivalents; further more, these code
    # are maily for dealing version differences between current ubuntu and
    # the ubuntu iso. So just remove them, a dirty but simple solution.
    patch -Np1 -i "${srcdir}/0002-remove-blackcode-using-lsb-release-and-python-debian.patch"

    # force using python2 as interpreter
    find . -type f | \
        xargs grep -l '^#!/usr/bin/python$' | \
                xargs sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_'

    python2 setup.py install --root="${pkgdir}"

    # FIXME:
    # That setup.py is written with the assumption that
    # /usr/share/kde4/apps is contained in the result of
    # $(kde-prefix --path data), which is true for ubuntu.
    #
    # But this assumption does not hold in archlinux, which will
    # make usb-creator-kde fail to load its ui file.
    #
    # So we must put usbcreator-kde.ui to an appropriate place!
    install --mode=0644 -D "${extractdir}/gui/usbcreator-kde.ui" \
                            "${pkgdir}/usr/share/apps/usb-creator-kde/usbcreator-kde.ui"

    # FIXME: install manpages, manually...
    local MANDIR="${pkgdir}/usr/share/man/man8"
    for manpage in ${pkgname}-{gtk,kde}.8 ; do
        install --mode=0644 -D "${extractdir}/man/${manpage}" "${MANDIR}/${manpage}"
    done

}

md5sums=('0e0efba4b531bd3af1e227a3cc10368f'
         'bc80c990f59f7cab2db8cfa76746be61'
         '290389e299688274592c5697578ba4cd')

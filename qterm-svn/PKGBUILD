# Maintainer: Xiaoan Wu <adaptee at gmail dot com>

pkgname=qterm-svn
pkgver=1286
pkgrel=1
pkgdesc="BBS client for Linux, written in Qt"
arch=('i686' 'x86_64')
url="http://www.qterm.org"
license=('GPL')
depends=('qt>=4.5.0' 'openssl')
makedepends=('cmake' 'subversion')
optdepends=('phonon')
conflicts=('qterm' 'qterm-unstable')
provides=('qterm')

_svntrunk='https://svn.qterm.org/svnroot/qterm/trunk/qterm-qt4'
_svnmod="qterm-qt4"

build()
{
    cd "${srcdir}"

    msg "Connecting to SVN server...."
    if [ -d "${_svnmod}/.svn" ]; then
        (cd "${_svnmod}" && svn update)
    else
        svn co "${_svntrunk}" "${_svnmod}"
    fi

    msg "Starting configuration..."

    rm -rf "${srcdir}/build"
    mkdir -p "${srcdir}/build"
    cd ${srcdir}/build || return 1

    cmake -DCMAKE_INSTALL_PREFIX=/usr -DQT_PHONON_INCLUDE_DIR=/usr/include/KDE ../${_svnmod} || return 1

    msg "Starting building..."
    make || return 1
}

package ()
{
    msg "Starting packing..."
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}/" install || return 1
}



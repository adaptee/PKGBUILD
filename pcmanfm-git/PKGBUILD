# Maintainer: TDY <tdy@gmx.com>

pkgname=pcmanfm-git
pkgver=20120915
pkgrel=1
pkgdesc="An extremely fast, lightweight, yet feature-rich file manager with tabbed browsing"
arch=('i686' 'x86_64')
url="http://pcmanfm.sourceforge.net/"
license=('GPL')
depends=('libfm-git')
makedepends=('git' 'intltool' 'pkg-config')
optdepends=('gvfs: mounting of local and remote drives'
            'gnome-menus: applications menu and "Open with..." dialog')
provides=('pcmanfm')
conflicts=('pcmanfm')
install=pcmanfm.install

_gitroot=git://pcmanfm.git.sourceforge.net/gitroot/pcmanfm/pcmanfm
_gitname=pcmanfm

build() {
  cd "$srcdir"

  msg "Connecting to $_gitroot..."
  if [[ -d $_gitname ]]; then
    cd $_gitname && git pull origin && cd ..
    msg2 "Local files updated"
  else
    git clone $_gitroot
    msg2 "Git checkout done"
  fi

  rm -rf $_gitname-build
  git clone $_gitname $_gitname-build
  cd $_gitname-build

  msg "Starting make..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make LDFLAGS="-lm ${LDFLAGS}"
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}

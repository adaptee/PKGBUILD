# Maintainer:  Jekyll Wu <adaptee at gmail dot com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Rick W. Chen <stuffcorpse@archlinux.us>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Marek Kubica <marek@xivilization.net>

realname=phonon
bename=mplayer
pkgname=${realname}-${bename}-git
pkgver=20111126
pkgrel=1
pkgdesc="Mplayer backend for phonon, the multimedia API of KDE."
arch=("i686" "x86_64")
url="http://projects.kde.org/projects/kdesupport/${realname}/${realname}-${bename}"
license=("LGPL")
depends=("${realname}" "${bename}")
makedepends=("pkg-config" "cmake" "automoc4" "git")
provides=("${realname}-${bename}" "${realname}-backend")
conflicts=("${realname}-${bename}" )
source=('fix_find_config.patch')
md5sums=('55a20d7146371fbf1f1139a2ed874604')

_gitroot="git://anongit.kde.org/${realname}-${bename}.git"
_gitname="${realname}-${bename}"

build() {

    # Config
    [ ${CARCH} = 'x86_64' ] && CXXFLAGS="$CXXFLAGS -fPIC"

    cd ${srcdir}

    # Repository
    msg "Connecting to GIT server...."
    if [ -d ${_gitname} ] ; then
        cd ${_gitname} && git pull origin
        msg "The local files are updated."
    else
        git clone ${_gitroot} ${_gitname}
        msg "GIT checkout done"
    fi


    if [ -d "${srcdir}/${_gitname}-build" ]; then
        rm "${srcdir}/${_gitname}-build/" -rf
    fi
    cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

    # Patch
    cd "${srcdir}/${_gitname}-build"
    patch -Np1 -i ${srcdir}/fix_find_config.patch

    # Build
    msg "Starting make..."

    { type -P kde4-config &>/dev/null && _prefix=$(kde4-config --prefix) ; } || _prefix=/usr
    cmake \
            -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=$_prefix \
            -DPLUGIN_INSTALL_DIR=$_prefix/lib/kde4 \
            -DSERVICES_INSTALL_DIR=$_prefix/share/kde4/services \
            ./

    make || return 1
}


package () {
    cd "${srcdir}/${_gitname}-build"
    make DESTDIR=${pkgdir} install || return 1
}
